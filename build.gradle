plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    // org.beryx.jlink is currently unmaintained, see: https://github.com/beryx/badass-jlink-plugin/issues/223
    // Fork with patches for newer JDKS:
    // https://www.reddit.com/r/java/comments/1g8o17x/patched_versions_of_badassjlink_and_badassruntime/
    id 'com.dua3.gradle.jlink' version '3.0.1-patch-1'
    id 'idea'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(23)

dependencies {
    implementation 'net.java.dev.jna:jna-platform-jpms:5.15.0'
}

javafx {
    version = '23.0.1'
    modules = ['javafx.graphics', 'javafx.controls']
    platform = 'win'
}

var compileJvmArgs = ['--enable-preview']
var runJvmArgs = compileJvmArgs + ['-XX:+UseZGC', '-Xlog:gc=debug::time']

tasks.withType(JavaCompile).forEach {
    it.options.compilerArgs.addAll compileJvmArgs
}

application {
    mainModule = 'dev.peksa.speedrun'
    mainClass = 'dev.peksa.speedrun.journey.app.Launcher'
    applicationDefaultJvmArgs.addAll runJvmArgs
}

jlink  {
    options = ['--strip-debug', '--compress', 'zip-9', '--no-header-files', '--no-man-pages', '--strip-native-commands',
               '--strip-java-debug-attributes', '--dedup-legal-notices=error-if-not-same-content']
    launcher {
        jvmArgs = runJvmArgs
        noConsole = false
        name = 'peksa-fancy-flyer-tool'
    }
    jpackage {
        imageOptions = ['--win-console', '--description', "Peksa's fancy flyer tool", '--copyright', 'Copyright (c) 2024 Peksa']
        vendor = 'Peksa'
        icon = 'icon.ico'
        version = '5.0.0'
        skipInstaller = true
        targetPlatformName = 'win'
    }
}

repositories {
    mavenCentral()
}